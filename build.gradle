buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://maven.google.com' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

// define dependency versions for this project
ext.deps = [
        advrecyclerview      : '0.10.5',
        androidSupport       : '25.3.1',
        androidTestingSupport: '0.5',
        crashlytics          : '2.6.8',
        facebookStetho       : '1.5.0',
        gson                 : '2.6.2',
        guava                : '22.0-android',
        hamcrest             : '1.3',
        jsonUnit             : '1.15.0',
        junit                : '4.12',
        leakcanary           : '1.5',
        lightweightStream    : '1.1.7',
        okhttp3              : '3.6.0',
        picasso              : '2.5.2',
        retrofit2            : '2.2.0',
        thekey               : '1.2.0'
]

subprojects {
    apply plugin: 'com.android.library'

    group 'org.ccci.gto.android'
    version '1.1.2-SNAPSHOT'

    repositories {
        mavenLocal()
        jcenter()
        maven {
            name "maven.gcx.org"
            url "https://maven.gcx.org/"
            credentials {
                username gcxMavenRepositoryUsername
                password gcxMavenRepositoryPassword
            }
        }
        maven { url 'https://maven.google.com' }
        maven { url 'https://maven.fabric.io/public' }
    }

    beforeEvaluate {
        android {
            compileSdkVersion 25
            buildToolsVersion "25.0.3"

            defaultConfig {
                minSdkVersion 9
                targetSdkVersion 25

                consumerProguardFiles "$rootProject.projectDir/proguard-consumer-jetbrains.pro"

                testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
            }

            adbOptions {
               timeOutInMs 600000
            }
            dexOptions {
                preDexLibraries !project.hasProperty('disablePreDex')
            }
        }

        dependencies {
            provided 'org.jetbrains:annotations:15.0'
            compile 'com.android.support:support-annotations:' + deps.androidSupport

            androidTestCompile 'com.android.support.test:runner:' + deps.androidTestingSupport
            androidTestCompile 'com.android.support.test:rules:' + deps.androidTestingSupport
            androidTestCompile 'org.hamcrest:hamcrest-library:' + deps.hamcrest
        }
    }

    // configure checkstyle
    apply plugin: 'checkstyle'
    checkstyle {
        toolVersion '7.0'
    }
    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('checkstyle.xml')
        source 'src'
        include '*/java/**/*.java'
        ignoreFailures false
        showViolations true

        classpath = files()
    }
    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }

    // configure maven uploadArchives task
    apply from: "$rootProject.projectDir/maven_publish.gradle"
    afterEvaluate { project ->
        artifacts {
            archives artifactSourcesJar
            archives artifactJavadocJar
        }
    }
}
