buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

// define dependency versions for this project
ext.deps = [
    androidSupport: '23.2.1'
]

subprojects {
    apply plugin: 'com.android.library'

    group 'org.ccci.gto.android'
    version '0.9.0-SNAPSHOT'

    repositories {
        mavenLocal()
        jcenter()
        maven {
            name "maven.gcx.org"
            url "https://maven.gcx.org/"
            credentials {
                username gcxMavenRepositoryUsername
                password gcxMavenRepositoryPassword
            }
        }
    }

    beforeEvaluate {
        android {
            compileSdkVersion 23
            buildToolsVersion "23.0.1"

            defaultConfig {
                minSdkVersion 8
                targetSdkVersion 23
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_7
                targetCompatibility JavaVersion.VERSION_1_7
            }
        }

        dependencies {
            compile 'com.android.support:support-annotations:' + deps.androidSupport
        }
    }

    apply from: "$rootProject.projectDir/maven_publish.gradle"
    afterEvaluate { project ->
        artifacts {
            archives artifactSourcesJar
            archives artifactJavadocJar
        }
    }
}
