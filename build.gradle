import org.jlleitschuh.gradle.ktlint.reporter.ReporterType

buildscript {
    // define dependency versions for this project
    ext.deps = [
            advrecyclerview     : '1.0.0',
            crashlytics         : '2.10.1',
            eventbus            : '3.1.1',
            facebookStetho      : '1.5.1',
            gradleAndroidPlugin : '3.4.2',
            gson                : '2.8.5',
            guava               : '28.0-android',
            hamcrest            : '2.1',
            jetbrainsAnnotations: '17.0.0',
            jsonUnit            : '1.31.1',
            junit               : '4.12',
            kotlin              : '1.3.41',
            leakcanary          : '1.6.3',
            lightweightStream   : '1.2.1',
            materialDesign      : '1.0.0',
            mockito             : '3.0.0',
            okhttp3             : '3.12.3',
            picasso             : '2.71828',
            powermock           : '2.0.2',
            realm               : '5.14.0',
            realmAdapters       : '3.1.0',
            retrofit2           : '2.6.1',
            snowplow            : '1.2.1',
            thekey              : '3.0.0',
            timber              : '4.7.1'
    ]
    ext.deps.androidX = [
            annotation        : '1.1.0',
            appCompat         : '1.0.2',
            collection        : '1.1.0',
            fragment          : '1.0.0',
            lifecycle         : '2.0.0',
            loader            : '1.0.0',
            recyclerView      : '1.0.0',
            room              : '2.1.0',
            swipeRefreshLayout: '1.0.0',
            test              : '1.2.0',
            testJUnit         : '1.1.1',
            viewPager         : '1.0.0',
    ]

    repositories {
        google()
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${deps.gradleAndroidPlugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${deps.kotlin}"
    }
}
plugins {
    id 'digital.wup.android-maven-publish' version '3.6.2' apply false
    id 'org.jlleitschuh.gradle.ktlint' version '8.2.0'
}

subprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'

    group 'org.ccci.gto.android'
    version '3.0.2-SNAPSHOT'

    repositories {
        google()
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url 'https://cruglobal.jfrog.io/cruglobal/list/maven-locals/' }
        mavenLocal()
    }

    beforeEvaluate {
        android {
            compileSdkVersion 28

            defaultConfig {
                minSdkVersion 16
                targetSdkVersion 28

                consumerProguardFiles "$rootProject.projectDir/proguard-consumer-jetbrains.pro"

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            }

            adbOptions {
               timeOutInMs 600000
            }
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            dexOptions {
                preDexLibraries !project.hasProperty('disablePreDex')
            }
        }

        dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${deps.kotlin}"

            compileOnly "androidx.annotation:annotation:${deps.androidX.annotation}"
            compileOnly "org.jetbrains:annotations:${deps.jetbrainsAnnotations}"

            androidTestImplementation "androidx.test:runner:${deps.androidX.test}"
            androidTestImplementation "androidx.test:rules:${deps.androidX.test}"
            androidTestImplementation "androidx.test.ext:junit:${deps.androidX.testJUnit}"
            androidTestImplementation "org.hamcrest:hamcrest-library:${deps.hamcrest}"
        }

        configurations.all {
            resolutionStrategy {
                force "androidx.annotation:annotation:${deps.androidX.annotation}"
                force "org.jetbrains:annotations:${deps.jetbrainsAnnotations}"

                dependencySubstitution {
                    substitute module('org.hamcrest:hamcrest-core') with module("org.hamcrest:hamcrest:${deps.hamcrest}")
                    substitute module('org.jetbrains.kotlin:kotlin-stdlib-jdk8') with module("org.jetbrains.kotlin:kotlin-stdlib-jdk7:${deps.kotlin}")
                }
            }
        }
    }

    // configure checkstyle
    apply plugin: 'checkstyle'
    checkstyle {
        toolVersion '7.8.2'
    }
    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('config/checkstyle/checkstyle.xml')
        source 'src'
        include '*/java/**/*.java'
        ignoreFailures false
        showViolations true

        classpath = files()
    }
    afterEvaluate {
        project.tasks.findByName("check")?.dependsOn('checkstyle')
    }

    // configure ktlint
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    ktlint {
        android = true
        reporters = [ReporterType.PLAIN_GROUP_BY_FILE, ReporterType.CHECKSTYLE]
    }

    // configure publishing
    apply plugin: 'digital.wup.android-maven-publish'
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
    }
    publishing {
        publications {
            release(MavenPublication) {
                from components.android
                pom.withXml {
                    def scm = asNode().appendNode('scm')
                    scm.appendNode('connection', POM_SCM_CONNECTION)
                    scm.appendNode('developerConnection', POM_SCM_CONNECTION)
                }

                artifact sourcesJar {
                    classifier 'sources'
                }
            }
        }
        repositories {
            maven {
                if (project.version.endsWith('-SNAPSHOT')) {
                    url "https://cruglobal.jfrog.io/cruglobal/list/maven-cru-android-public-snapshots-local/"
                } else {
                    url "https://cruglobal.jfrog.io/cruglobal/list/maven-cru-android-public-releases-local/"
                }

                credentials {
                    username cruGlobalMavenRepositoryUsername
                    password cruGlobalMavenRepositoryPassword
                }
            }
        }
    }
}
