buildscript {
    // define dependency versions for this project
    ext.deps = [
            advrecyclerview      : '0.11.0',
            androidXAnnotation   : '1.0.0',
            androidXAppCompat    : '1.0.0',
            androidXCollection   : '1.0.0',
            androidXDataBinding  : '3.2.0',
            androidXFragment     : '1.0.0',
            androidXLoader       : '1.0.0',
            androidXRecyclerView : '1.0.0',
            androidXRoom         : '2.0.0-rc01',
            androidXSwipeRefreshLayout: '1.0.0',
            androidXTest         : '1.0.0',
            androidXViewPager         : '1.0.0',
            crashlytics          : '2.9.5',
            eventbus             : '3.1.1',
            facebookStetho       : '1.5.0',
            gson                 : '2.8.5',
            guava                : '26.0-android',
            hamcrest             : '1.3',
            jsonUnit             : '1.31.1',
            junit                : '4.12',
            leakcanary           : '1.6.1',
            lightweightStream    : '1.2.1',
            materialDesign       : '1.0.0',
            mockito              : '2.18.0',
            okhttp3              : '3.11.0',
            picasso              : '2.5.2',
            powermock            : '2.0.0-beta.9',
            retrofit2            : '2.4.0',
            snowplow             : '0.8.0',
            thekey               : '2.0.0',
            timber               : '4.7.1'
    ]

    repositories {
        google()
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0'
    }
}
plugins {
    id 'digital.wup.android-maven-publish' version '3.6.2' apply false
}

subprojects {
    apply plugin: 'com.android.library'

    group 'org.ccci.gto.android'
    version '2.0.0-SNAPSHOT'

    repositories {
        google()
        maven { url 'https://maven.fabric.io/public' }
        // XXX: temporary to use beta version of powermock
        maven { url 'https://dl.bintray.com/powermock/maven' }
        jcenter()
        maven { url 'https://cruglobal.jfrog.io/cruglobal/list/maven-locals/' }
        mavenLocal()
    }

    beforeEvaluate {
        android {
            compileSdkVersion 28

            defaultConfig {
                minSdkVersion 14
                targetSdkVersion 28

                consumerProguardFiles "$rootProject.projectDir/proguard-consumer-jetbrains.pro"

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            }

            adbOptions {
               timeOutInMs 600000
            }
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            dexOptions {
                preDexLibraries !project.hasProperty('disablePreDex')
            }
        }

        dependencies {
            compileOnly "androidx.annotation:annotation:${deps.androidXAnnotation}"
            compileOnly 'org.jetbrains:annotations:16.0.1'

            androidTestImplementation "androidx.test:runner:${deps.androidXTest}"
            androidTestImplementation "androidx.test:rules:${deps.androidXTest}"
            androidTestImplementation "com.android.support.test:rules:${deps.androidTestingSupport}"
            androidTestImplementation "org.hamcrest:hamcrest-library:${deps.hamcrest}"
        }
    }

    // configure checkstyle
    apply plugin: 'checkstyle'
    checkstyle {
        toolVersion '7.8.2'
    }
    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('config/checkstyle/checkstyle.xml')
        source 'src'
        include '*/java/**/*.java'
        ignoreFailures false
        showViolations true

        classpath = files()
    }
    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }

    // configure publishing
    apply plugin: 'digital.wup.android-maven-publish'
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
    }
    publishing {
        publications {
            release(MavenPublication) {
                from components.android
                pom.withXml {
                    def scm = asNode().appendNode('scm')
                    scm.appendNode('connection', POM_SCM_CONNECTION)
                    scm.appendNode('developerConnection', POM_SCM_CONNECTION)
                }

                artifact sourcesJar {
                    classifier 'sources'
                }
            }
        }
        repositories {
            maven {
                if (project.version.endsWith('-SNAPSHOT')) {
                    url "https://cruglobal.jfrog.io/cruglobal/list/maven-cru-android-public-snapshots-local/"
                } else {
                    url "https://cruglobal.jfrog.io/cruglobal/list/maven-cru-android-public-releases-local/"
                }

                credentials {
                    username cruGlobalMavenRepositoryUsername
                    password cruGlobalMavenRepositoryPassword
                }
            }
        }
    }
}
