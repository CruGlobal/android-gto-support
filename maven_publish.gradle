apply plugin: 'maven'

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "https://maven.gcx.org/") {
                authentication(userName: gcxMavenRepositoryUsername, password: gcxMavenRepositoryPassword)
            }
            snapshotRepository(url: "https://maven.gcx.org/") {
                authentication(userName: gcxMavenRepositoryUsername, password: gcxMavenRepositoryPassword)
            }

            pom.project {
                scm {
                    connection POM_SCM_CONNECTION
                    developerConnection POM_SCM_CONNECTION
                }
            }
        }
    }
}

afterEvaluate {
    if (project.plugins.hasPlugin('java')) {
        task artifactSourcesJar(type: Jar) {
            from sourceSets.main.allSource
            classifier = 'sources'
        }

        task artifactJavadocJar(type: Jar, dependsOn: 'javadoc') {
            from javadoc.destinationDir
            classifier = 'javadoc'
        }
    } else if (project.plugins.hasPlugin('com.android.library')) {
        apply plugin: 'com.github.dcendents.android-maven'

        task artifactSourcesJar(type: Jar) {
            classifier = 'sources'
            from android.sourceSets.main.java.srcDirs
        }

        task javadoc(type: Javadoc) {
            source = android.sourceSets.main.java.srcDirs
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        }

        task artifactJavadocJar(type: Jar, dependsOn: 'javadoc') {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }
    }
}
